apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'

version = version_readable
group = "com.awareframework"

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.1"

    defaultConfig {
        applicationId "com.aware.plugin.bluetooth_beacon_detect" //EDIT ME
        minSdkVersion 17
        targetSdkVersion 24
        versionCode version_code
        versionName version_readable
    }

    signingConfigs {
        release {
            //set values in ~/.gradle/gradle.properties to sign with your own keystore and certificate
            storeFile file(project.hasProperty("KEYSTORE_FILE") ? KEYSTORE_FILE : "/")
            storePassword project.hasProperty("KEYSTORE_PASSWORD") ? KEYSTORE_PASSWORD : ""
            keyAlias project.hasProperty("KEY") ? KEY : ""
            keyPassword project.hasProperty("KEY_PASSWORD") ? KEY_PASSWORD : ""
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    lintOptions {
        abortOnError false
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

bintray {
    user = project.hasProperty("bintrayUsername") ? bintrayUsername : ""
    key = project.hasProperty("bintrayKey") ? bintrayKey : ""

    configurations = ['archives']

    publish = true

    pkg {
        repo = 'com.awareframework'
        name = android.defaultConfig.applicationId
        licenses = ['Apache-2.0']
        websiteUrl = 'https://github.com/akuvisur/bluetooth_beacon_detect' //EDIT ME
        issueTrackerUrl = 'https://github.com/akuvisur/bluetooth_beacon_detect/issues' //EDIT ME
        labels = ['aar', 'android', 'framework', 'aware']
        publicDownloadNumbers = true
        vcsUrl = 'https://github.com/akuvisur/bluetooth_beacon_detect.git' //EDIT ME
        desc = "AWARE Plugin: Bluetooth Beacon Detect" //EDIT ME
        userOrg = user
        githubRepo = "akuvisur/bluetooth_beacon_detect" //EDIT ME

        version {
            name = "$version_readable"
            desc = bintray.pkg.desc
            released = new Date()
            vcsTag = "$version_readable"
        }
    }
}

task generateSourcesJar(type: Jar) {
   from android.sourceSets.main.java.srcDirs
   classifier 'sources'
}

task generateJavadocs(type: Javadoc) {
   failOnError false
   source = android.sourceSets.main.java.srcDirs
   classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task generateJavadocsJar(type: Jar) {
   from generateJavadocs.destinationDir
   classifier 'javadoc'
}

generateJavadocsJar.dependsOn generateJavadocs

artifacts {
   archives generateJavadocsJar
   archives generateSourcesJar
}

dependencies {
    compile 'com.github.denzilferreira:aware-client:development-SNAPSHOT'
    compile "com.android.support:appcompat-v7:${support_libs}"
    compile 'org.altbeacon:android-beacon-library:2.9.2'
    compile 'com.android.support:design:24.2.1'
}
